! COMPUTES BLUE
SUBROUTINE BLUE_COMP2(DATESTRING, ASSIMILATION_POINT, DAINFLUENCE,   &
                      PMATRIX_R1, PMATRIX_R2, PMATRIX_R3, PMATRIX_A)
    IMPLICIT NONE
    INTEGER,PARAMETER::IP=4, WP=8
    INTEGER,PARAMETER::VPBINS=40   ! Read in VPBINS AND NCELLS
    INTEGER NCELLS, ASSIMILATION_POINT, DAINFLUENCE
    INTEGER(IP) EFDCSIZE
    INTEGER(IP) NR,NL,II,ATOT,I,J,SZVP,FILEND,LDA,M,N,LWORK,INFO
    CHARACTER(LEN=1084) DATESTRING, DA_NAME, VPSTEM(2), OUTSTEM
    INTEGER(IP),ALLOCATABLE,DIMENSION(:)::MODEL_I,MODEL_J,MODEL_K,VP_I,VP_J,VP_K,IPIV
    INTEGER(IP),ALLOCATABLE,DIMENSION(:,:)::H
    REAL(WP),ALLOCATABLE,DIMENSION(:)::TEMP_EFDC,SIGMAT,TEMP_VP,KH,YINN,WORK
    REAL(WP),ALLOCATABLE,DIMENSION(:,:):: AUX
    REAL(WP),ALLOCATABLE,DIMENSION(:,:)::P,R
    REAL(WP),ALLOCATABLE,DIMENSION(:,:) :: YINNINV,A
    REAL(WP),ALLOCATABLE,DIMENSION(:):: YF
    REAL PMATRIX_R1, PMATRIX_R2, PMATRIX_R3, PMATRIX_A
    CHARACTER(LEN=1084) efdcfile
    CHARACTER(LEN=1084) vpfile, vpfile_path
    CHARACTER(LEN=1084) outfile
    CHARACTER(LEN=2) VPID
    ! FILENAME STEM FOR THE TWO VP DATASETS
    VPSTEM(1) = "TIVP"    ! 1) TEA ISLAND
    VPSTEM(2) = "CPVP"    ! 2) CALVES PEN
    vpfile_path = '/jp4/tjpcm/ernesto/Simulations_dataAssim/VerticalProfilerDataAssimFiles/'
    ! READ THE DATA
    WRITE(VPID,'(I2.2)') ASSIMILATION_POINT
    EFDCFILE = 'EFDC_JPVP'// VPID // '-'  // trim(DATESTRING) // '.txt'
    VPFILE = trim(vpfile_path) // trim(VPSTEM(ASSIMILATION_POINT)) // trim(DATESTRING) // '.csv'
    OUTFILE =trim(VPSTEM(ASSIMILATION_POINT)) // 'DACOMPUTED-'  // trim(DATESTRING) // '.csv'
    write(*,*) 'VPNAME = ', trim(EFDCFILE), trim(VPFILE) 
    OPEN(1,FILE=trim(efdcfile),STATUS='OLD')  ! Model states
    OPEN(2,FILE=trim(vpfile),STATUS='OLD')    ! Observaton file

    NR = 0
    NL = 0
    NCELLS = (DAINFLUENCE * 2) + 1  ! DAINFLUENCE READ FROM DA.INP AND DICTATES # CELLS EITHER SIDE

    !! READ THE EFDC DATA
    EFDCSIZE = VPBINS * (NCELLS**2)
    !ATOT = 4000
    ALLOCATE(MODEL_I(EFDCSIZE))
    ALLOCATE(MODEL_J(EFDCSIZE))
    ALLOCATE(MODEL_K(EFDCSIZE))
    ALLOCATE(TEMP_EFDC(EFDCSIZE))
    DO II=1,EFDCSIZE           ! READ EFDC FILE
        READ(1,*,IOSTAT=FILEND) MODEL_I(II),MODEL_J(II),MODEL_K(II),TEMP_EFDC(II)
        ! WE PROBABLY DON'T NEED THIS ANYMORE AS WE KNOW IN ADVANCE THE SIZE OF THE DATASETS
        IF (FILEND.LT.0)GOTO 111
        !  PRINT *,TEMP_EFDC(II)
        NL = NL + 1           ! SIZE OF  EFDC
    END DO
111 CONTINUE
    PRINT *,NL
    CLOSE(1)

    ! READ THE VP DATA
    SZVP = VPBINS
    ALLOCATE(TEMP_VP(SZVP))
    ALLOCATE(SIGMAT(SZVP))
    ALLOCATE(VP_I(SZVP))
    ALLOCATE(VP_J(SZVP))
    ALLOCATE(VP_K(SZVP))
    DO II = 1,SZVP         ! READ VP DATA TO UNKNOWN FILE END
        READ(2,*,IOSTAT=FILEND) VP_I(II),VP_J(II),VP_K(II),TEMP_VP(II),SIGMAT(II)
        IF (FILEND.LT.0)GOTO 222
        !  PRINT *,TEMP_VP(II)
        !PRINT *,NR
        NR = NR + 1           ! SIZE OF VP
    END DO
222 CONTINUE
    PRINT *,NR
    CLOSE(2)

    ! BUILD DA MATRICES
    ALLOCATE(H(NR,NL))
    CALL HMATRIX2(NL,NR,VP_I,VP_J,VP_K,MODEL_I,MODEL_J,MODEL_K,H)
    ALLOCATE(P(NL,NL))
    CALL PMATRIX2(NL,MODEL_I,MODEL_J,MODEL_K,P, &
                  PMATRIX_R1, PMATRIX_R2, PMATRIX_R3, PMATRIX_A )
    ALLOCATE(R(NR,NR))
    CALL RMATRIX2(NR,SIGMAT,R)
    PRINT *,"Matrices built."

    ! COMPUTE BLUE

    ALLOCATE(AUX(NR,NL))
    ALLOCATE(YINNINV(NR,NR))
    ALLOCATE(KH(NR))
    ALLOCATE(YINN(NR))

    YINN = TEMP_VP - MATMUL(H,TEMP_EFDC)
    PRINT *,"Innovation computed."
    AUX = MATMUL(H,P)
    YINNINV = MATMUL(AUX,TRANSPOSE(H)) + R
    PRINT *,"Matrix to invert computed."

    ! COMPUTE THE INVERSE OF YINN USING LAPACK
    ! DGETRF COMPUTES AN LU FACTORIZATION OF A GENERAL M-BY-N MATRIX A
    ! USING PARTIAL PIVOTING WITH ROW EXCHANGE

    LDA = MAX(1,NR)
    LWORK = NR*NR

    ALLOCATE(A(NR,NR))
    ALLOCATE(IPIV(NR))
    ALLOCATE(WORK(LWORK))
    A = YINNINV
    CALL DGETRF(NR,NR,A,LDA,IPIV,INFO)
    IF (info == 0) WRITE(*,*) 'LU decomposition successful'
    IF (info < 0 ) WRITE(*,11) INFO,INFO
11  FORMAT('LU decomposition: U(',I4,',',I4,') = 0)')

    CALL DGETRI(NR,A,LDA,IPIV,WORK,LWORK,INFO)

    IF (INFO /= 0 ) THEN
        STOP 'Matrix Inversion Failed'
    ELSE
        WRITE(*,*) 'Inverse Succesful'
    END IF

    ALLOCATE(YF(NL))
    IF (ALLOCATED (AUX)) DEALLOCATE (AUX)
    ALLOCATE(AUX(NL,NR))

    KH = MATMUL(A,YINN)
    AUX = MATMUL(P,TRANSPOSE(H))
    YF = TEMP_EFDC + MATMUL(AUX,KH)

    OPEN(4,FILE=trim(outfile),STATUS='UNKNOWN')
    !OPEN(4,FILE='blue.csv',STATUS='UNKNOWN')
    DO I = 1,NL
        WRITE(4,4) MODEL_I(I),MODEL_J(I),MODEL_K(I),YF(I),TEMP_EFDC(I)
    END DO
    CLOSE(4)
4   FORMAT(3I5, 2 f12.6)
END SUBROUTINE BLUE_COMP2


